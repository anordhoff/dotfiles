#-----------------------------
# general
#-----------------------------

# start tmux on shell launch, attaching to (or creating a) session 'dotfiles'
[ -z "${TMUX}" ] && tmux new-session -A -s dotfiles

# options
setopt   dotglob      # include hidden files in expansion and completion
setopt   extendedglob # treat '#', '~', and '^' as part of patterns
setopt   nomatch      # print an error if no filename matches pattern
unsetopt flowcontrol  # disable suspending terminal with ctrl-s

# directory stack options 
setopt autopushd       # make cd push the old directory onto the dir stack
setopt pushdignoredups # do not push copies of the same dir onto the dir stack
setopt pushdsilent     # do not print the dir stack after pushd or popd

# disable beeping
unsetopt beep
unsetopt histbeep
unsetopt listbeep

# print current working directory and exit code in the prompt
PS1='%F{blue}%60<...<%~%F{default}%(?.. (%?%)) $ '

# use emacs mode for zle
bindkey -e

# bash-like navigation
autoload -Uz select-word-style
select-word-style bash

# delete key
bindkey "^[[3~" delete-char

# delete the line before the cursor, rather than entire line (to mimic bash)
bindkey '^U' backward-kill-line

# CTRL-X_CTRL-E opens the current command in the editor
autoload -Uz edit-command-line
zle -N edit-command-line
bindkey '^X^E' edit-command-line

# use 'help' command for zsh built-ins
autoload -Uz run-help

# lazyload functions in the *files/zsh/functions directory
typeset -U fpath
fpath=(~/dotfiles/zsh/functions ~/jobfiles/zsh/functions $fpath)
autoload ${fpath[1]}/*(:t)
autoload ${fpath[2]}/*(:t)

# add completion directories to fpath
fpath=(~/dotfiles/zsh/completions $fpath)
fpath=(~/jobfiles/zsh/completions $fpath)

# enable fzf
if [[ "${OSTYPE}" =~ "darwin"* ]]; then
  [ -f /usr/local/opt/fzf/shell/completion.zsh ] &&
    source /usr/local/opt/fzf/shell/completion.zsh
  [ -f /usr/local/opt/fzf/shell/key-bindings.zsh ] &&
    source /usr/local/opt/fzf/shell/key-bindings.zsh
else
  [ -f /usr/share/fzf/shell/completion.zsh ] &&
    source /usr/share/fzf/shell/completion.zsh
  [ -f /usr/share/fzf/shell/key-bindings.zsh ] &&
    source /usr/share/fzf/shell/key-bindings.zsh
fi

# enable expand-multiple-dots.zsh
[ -f ~/dotfiles/zsh/extensions/expand-multiple-dots.zsh ] &&
	source ~/dotfiles/zsh/extensions/expand-multiple-dots.zsh

# enable tab-on-empty-line-shows-files.zsh
[ -f ~/dotfiles/zsh/extensions/expand-or-complete-or-list-files.zsh ] &&
	source ~/dotfiles/zsh/extensions/expand-or-complete-or-list-files.zsh


#-----------------------------
# history
#-----------------------------

HISTFILE=~/.zhistory
HISTSIZE=10000
SAVEHIST=10000

setopt extendedhistory   # save start time and length of time commands run
setopt histignorealldups # keep only the most recently added duplicate command
setopt histignorespace   # commands that start with a space are not written
setopt histnostore       # history and fc commands are not written to history
setopt histreduceblanks  # remove excess spaces before writing a comman
setopt incappendhistory  # update the history file after each executed command

# match typed command and arguments when moving through history
autoload -Uz up-line-or-beginning-search
autoload -Uz down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey '^[[A' up-line-or-beginning-search
bindkey '^[[B' down-line-or-beginning-search
bindkey '^P' up-line-or-beginning-search
bindkey '^N' down-line-or-beginning-search


#-----------------------------
# completion
#-----------------------------

setopt   alwaystoend    # completion in a word will move cursor to end of word
setopt   completeinword # completion takes place at the cursor position in word
unsetopt listtypes      # don't show filetypes in completion menu

# load the completion module
autoload -Uz compinit; compinit
zmodload -i zsh/complist

# automatically load bash completion functions
autoload -U +X bashcompinit && bashcompinit

# go backwards in completion menu using shift-tab
bindkey -M menuselect '^[[Z' reverse-menu-complete

# complete *. with extensions; standard; one error; case insensitive completion
zstyle ':completion:*' completer \
	_extensions _complete _approximate _complete:-extended

# allow one error
zstyle ':completion:*' max-errors 1

# smart case completion; complete partial words
zstyle ':completion:*:complete-extended:*' \
	matcher-list 'm:{a-z-_}={A-Z_-}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'

# cache completions
zstyle ':completion:*' use-cache true
zstyle ':completion:*' cache-path ~/.zcompcache

# use a completion menu
zstyle ':completion:*' menu select

# group matches based on type
zstyle ':completion:*' group-name ''

# list directories first when completing
zstyle ':completion:*' list-dirs-first true

# improve the style of the completion menu
zstyle ':completion:*' select-prompt '-- scrolling active (%p) --'
zstyle ':completion:*:descriptions' format '%F{magenta}-- %d --%f'
zstyle ':completion:*:messages' format '-- %d --'
zstyle ':completion:*:warnings' format '-- no matches found --'

# use colors in completion menu; set directory color to white
LS_COLORS_COMPLETION=$(echo $LS_COLORS | sed "s/di=[0-9]\{2\}/di=37/")
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS_COMPLETION}
unset LS_COLORS_COMPLETION


#-----------------------------
# misc
#-----------------------------

# source work zshrc
[ -r ~/jobfiles/zsh/zshrc ] && source ~/jobfiles/zsh/zshrc
